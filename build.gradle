buildscript {
  repositories {
    maven {
        url "https://repo.spring.io/plugins-release/"
    }
    mavenCentral()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.10.RELEASE"
    classpath 'io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE'
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
  }
}

apply plugin: "java"
apply plugin: "application"
apply plugin: "org.springframework.boot"
apply plugin: "com.google.protobuf"

ext {
    grpcVersion = "1.10.0"
}

archivesBaseName = 'flr-api'
version = '0.0.1'

repositories {
    maven {
        url "https://maven.repository.redhat.com/ga/"
    }
    maven {
        url "https://repository.jboss.org/nexus/content/groups/public"
    }
    maven {
        url "https://maven.repository.redhat.com/earlyaccess/all"
    }
   	mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter'
    compile('org.lognet:grpc-spring-boot-starter:2.1.5') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter'
    }

    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        compile 'javax.annotation:javax.annotation-api:1.3.1'
    }

    testCompile 'junit:junit:4.12'
}

dependencyManagement {
  imports {
    mavenBom 'io.openshift.booster:spring-boot-booster-parent:1.5.10-9-rhoar'
  }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.2.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.4.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

springBoot {
  executable = true
}

jar {
    archiveName = "app.jar"
}
